// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
    // Increment if necessary
    Inc16(in=baseline, out=incremented);
    Mux16(a=baseline, b=incremented, sel=inc, out=updatedNumber);
    // Override with new number if loading
    Mux16(a=updatedNumber, b=in, sel=load, out=current);
    // Override with zero if reset
    Mux16(a=current, b=false, sel=reset, out=zeroedCurrent);

    // Store the new number, then project as output and new baseline
    Register(in=zeroedCurrent, load=true, out=baseline, out=out);
}
